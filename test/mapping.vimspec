Describe gram/mapping.vim
  Before all
    call gram#mapping#add_mode('testmode')
  End

  Before each
    call gram#mapping#_clear_entire_mapping()
    call gram#mapping#_clear_input_queue()
    call gram#mapping#add_mode('testmode')
  End

  Describe add_typed_key()
    It adds given string to input_queue
      call gram#mapping#add_typed_key('a')
      Assert Equals(gram#mapping#_get_input_queue(), 'a')
      call gram#mapping#add_typed_key('b')
      Assert Equals(gram#mapping#_get_input_queue(), 'ab')
    End
  End

  Describe switch_mode()
    It sets the current mode
      call gram#mapping#switch_mode('normal')
      Assert Equals(gram#mapping#get_mode(), 'normal')
      call gram#mapping#switch_mode('insert')
      Assert Equals(gram#mapping#get_mode(), 'insert')
    End
  End

  Describe add_mode()
    After each
      call gram#mapping#_clear_entire_mapping()
    End
    It adds mode
      call gram#mapping#_clear_entire_mapping()
      Assert Equals(gram#mapping#_get_maptree_sets(), {})
      call gram#mapping#add_mode('testmode')
      Assert Equals(gram#mapping#_get_maptree_sets(), {'testmode': {}})
    End
  End

  Describe noremap()
    Before each
      call gram#mapping#_clear_entire_mapping()
      call gram#mapping#add_mode('testmode')
      call gram#mapping#switch_mode('testmode')
    End

    After all
      call gram#mapping#_clear_entire_mapping()
    End

    It adds mapping (depth = 1)
      call gram#mapping#noremap('testmode', 'a', 'testmode-a')
      Assert Equals(gram#mapping#_get_maptree_sets(),
            \{'testmode': {'a': {'rhs': {'nomore': 1, 'mapto': 'testmode-a'}}}})
    End

    It adds mapping (depth = 2)
      call gram#mapping#noremap('testmode', 'ab', 'testmode-ab')
      Assert Equals(gram#mapping#_get_maptree_sets(),
            \{'testmode': {'a': {'b': {'rhs': {'nomore': 1, 'mapto': 'testmode-ab'}}}}})
    End
  End

  Describe lookup_mapping()
    Before each
        call gram#mapping#switch_mode('testmode')
    End

    Context noremap only
      It finds mapping 1
        call gram#mapping#noremap('testmode', 'a', 'testmode-a')
        call gram#mapping#add_typed_key('a')
        Assert Equals(gram#mapping#lookup_mapping(), 'testmode-a')
        Assert Equals(gram#mapping#_get_input_queue(), '')
      End

      It finds mapping 2
        call gram#mapping#noremap('testmode', 'ab', 'testmode-ab')
        call gram#mapping#add_typed_key('ab')
        Assert Equals(gram#mapping#lookup_mapping(), 'testmode-ab')
        Assert Equals(gram#mapping#_get_input_queue(), '')
      End

      It finds mapping 3
        call gram#mapping#noremap('testmode', 'a', 'testmode-a')
        call gram#mapping#noremap('testmode', 'ab', 'testmode-ab')
        call gram#mapping#add_typed_key('ab')
        Assert Equals(gram#mapping#lookup_mapping(), 'testmode-ab')
        Assert Equals(gram#mapping#_get_input_queue(), '')
      End

      It finds mapping 4
        call gram#mapping#noremap('testmode', 'a', 'testmode-a')
        call gram#mapping#add_typed_key('abc')
        Assert Equals(gram#mapping#lookup_mapping(), 'testmode-a')
        Assert Equals(gram#mapping#_get_input_queue(), 'bc')
      End

      It finds the mapping not exist 1
        call gram#mapping#noremap('testmode', 'a', 'testmode-a')
        call gram#mapping#add_typed_key('b')
        Assert Equals(gram#mapping#lookup_mapping(), 'b')
        Assert Equals(gram#mapping#_get_input_queue(), '')
      End

      It finds the mapping not exist 2
        call gram#mapping#add_typed_key('a')
        Assert Equals(gram#mapping#lookup_mapping(), 'a')
        Assert Equals(gram#mapping#_get_input_queue(), '')
      End

      It waits for more key typed
        call gram#mapping#noremap('testmode', 'a', 'testmode-a')
        call gram#mapping#noremap('testmode', 'ab', 'testmode-ab')
        call gram#mapping#add_typed_key('a')
        Assert Equals(gram#mapping#lookup_mapping(), '')
        Assert Equals(gram#mapping#_get_input_queue(), 'a')
      End
    End

    Context noremap and map are mixtured
      It finds mapping
        call gram#mapping#map('testmode', 'a', 'testmode-map-a')
        call gram#mapping#noremap('testmode', 'testmode-map-a', 'testmode-noremap-a')
        call gram#mapping#add_typed_key('a')
        Assert Equals(gram#mapping#lookup_mapping(), 'testmode-noremap-a')
        Assert Equals(gram#mapping#_get_input_queue(), '')
      End

      It finds the remaped rhs not exist
        call gram#mapping#map('testmode', 'a', 'testmode-map-a')
        call gram#mapping#add_typed_key('a')
        Assert Equals(gram#mapping#lookup_mapping(), 't')
        Assert Equals(gram#mapping#_get_input_queue(), 'estmode-map-a')
      End
    End
  End

  Describe unmap()
    Before each
      call gram#mapping#_clear_entire_mapping()
      call gram#mapping#add_mode('testmode')
      call gram#mapping#switch_mode('testmode')
    End

    After all
      call gram#mapping#_clear_entire_mapping()
    End

    It removes mapping 1
      call gram#mapping#noremap('testmode', 'abc', 'rhs-abc')
      call gram#mapping#unmap('testmode', 'abc')
      Assert Equals(gram#mapping#_get_maptree_sets(), {'testmode': {}})
    End

    It removes mapping 2
      call gram#mapping#noremap('testmode', 'abc', 'rhs-abc')
      call gram#mapping#noremap('testmode', 'abd', 'rhs-abd')
      call gram#mapping#unmap('testmode', 'abc')

      call gram#mapping#add_typed_key('abc')
      Assert Equals(gram#mapping#lookup_mapping(), 'abc')
      Assert Equals(gram#mapping#_get_input_queue(), '')

      call gram#mapping#_clear_input_queue()
      call gram#mapping#add_typed_key('abd')
      Assert Equals(gram#mapping#lookup_mapping(), 'rhs-abd')
      Assert Equals(gram#mapping#_get_input_queue(), '')
      Assert Equals(gram#mapping#_get_maptree_sets(), {'testmode':
            \ {'a': {'b': {'d': {'rhs': {'nomore': 1, 'mapto': 'rhs-abd'}}}}}
            \ })
    End

    It removes mapping 3
      call gram#mapping#noremap('testmode', 'abcefg', 'rhs-abcefg')
      call gram#mapping#noremap('testmode', 'abd', 'rhs-abd')
      call gram#mapping#unmap('testmode', 'abcefg')

      call gram#mapping#add_typed_key('abcefg')
      Assert Equals(gram#mapping#lookup_mapping(), 'abc')
      Assert Equals(gram#mapping#_get_input_queue(), 'efg')

      call gram#mapping#_clear_input_queue()
      call gram#mapping#add_typed_key('abd')
      Assert Equals(gram#mapping#lookup_mapping(), 'rhs-abd')
      Assert Equals(gram#mapping#_get_input_queue(), '')
      Assert Equals(gram#mapping#_get_maptree_sets(), {'testmode':
            \ {'a': {'b': {'d': {'rhs': {'nomore': 1, 'mapto': 'rhs-abd'}}}}}
            \ })
    End

  End

  Describe timeoutlen
    Before all
      let g:backed = ''
      function TimeoutlenCallback(s)
        let g:backed = a:s
      endfunction
      call gram#mapping#set_callback_on_timeout('TimeoutlenCallback')
    End

    After all
      unlet g:backed
      delfunc TimeoutlenCallback
    End

    Before each
      call gram#mapping#set_timeoutlen(1)
      call gram#mapping#_clear_input_queue()
    End

    After each
      call gram#mapping#stop_timeoutlen_timer()
      let g:backed = ''
    End

    It notifies timeout
      call gram#mapping#add_typed_key('abc')
      call gram#mapping#start_timeoutlen_timer()
      sleep 30m
      Assert Equals(g:backed, 'abc')
      Assert Equals(gram#mapping#_get_input_queue(), '')
    End

    It quits working if stop_timeoutlen_timer() is called
      call gram#mapping#set_timeoutlen(10)
      call gram#mapping#add_typed_key('abc')
      call gram#mapping#start_timeoutlen_timer()
      call gram#mapping#stop_timeoutlen_timer()
      sleep 30m
      Assert Equals(g:backed, '')
      Assert Equals(gram#mapping#_get_input_queue(), 'abc')
    End

    It does not call callback when the input queue is empty
      let g:backed = 'initial'
      call gram#mapping#start_timeoutlen_timer()
      sleep 30m
      Assert Equals(g:backed, 'initial')
    End
  End
End
